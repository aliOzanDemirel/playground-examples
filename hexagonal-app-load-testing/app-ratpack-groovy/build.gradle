plugins {
    id "io.ratpack.ratpack-groovy" version "1.9.0"
}

// current ratpack version cannot run on java 16
compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {

    implementation(project(":business-logic"))

    ratpack.dependency 'ratpack-core'
    ratpack.dependency 'ratpack-guice'

    // slf4j-api comes from common business-logic, explicitly defining binding here
    runtimeOnly "org.slf4j:slf4j-api:1.7.32"
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'

    // using core API to programmatically modify log levels of loggers
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'

    // ratpack fails (missing field in a class) with latest language version of groovy, used version from ratpack dependency is 2.5.4
    // implementation 'org.codehaus.groovy:groovy-all:3.0.8'
}

run {
    systemProperties System.getProperties()
}

jar {
    manifest {
        attributes 'Main-Class': 'app.Main'
    }
    archiveFileName = 'app.jar'
    duplicatesStrategy DuplicatesStrategy.WARN
    from { configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) } }
}