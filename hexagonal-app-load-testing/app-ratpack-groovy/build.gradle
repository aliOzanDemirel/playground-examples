plugins {
    id "io.ratpack.ratpack-groovy" version "1.9.0"
}

// ratpack 1.9 and its groovy lang is not compatible with common java version (17) used in other app examples
compileGroovy {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {

    implementation(project(":business-logic"))

    ratpack.dependency 'ratpack-core'
    ratpack.dependency 'ratpack-guice'

    // slf4j-api comes from business.common business-logic, explicitly defining binding here
    runtimeOnly "org.slf4j:slf4j-api:1.7.32"
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'

    // using core API to programmatically modify log levels of loggers
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'

    // ratpack fails with groovy version 3.0.9 (and some other lesser versions), reason is missing field 'verifier' in class 'CompilationUnit'
    // ratpack 1.9.0 uses groovy language 2.5.4, it fails to compile with versions 2.5.10+
    implementation 'org.codehaus.groovy:groovy-all:2.5.9'
}

run {
    systemProperties System.getProperties()
}

jar {
    manifest {
        attributes 'Main-Class': 'app.Main'
    }
    archiveFileName = 'app.jar'
    duplicatesStrategy DuplicatesStrategy.WARN
    from { configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) } }
}