plugins {
    id 'org.springframework.boot' version '2.1.8.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

springBoot {
    // spring boot plugin injects the version and build information into actuator /info endpoint
    buildInfo()
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

configurations {
    implementation {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

dependencies {

    compileOnly 'org.projectlombok:lombok:1.18.10'
    testCompileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'

    implementation 'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-log4j2',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.springframework.boot:spring-boot-starter-amqp',
            'io.springfox:springfox-swagger-ui:2.9.2',
            'io.springfox:springfox-swagger2:2.9.2',
            'com.integralblue:log4jdbc-spring-boot-starter:2.0.0',
            'org.hibernate:hibernate-core:5.4.8.Final',
            'com.h2database:h2:1.4.200'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
    archiveFileName = 'bond-issuer.jar'
    mainClassName 'bond.Main'
    manifest {
        attributes 'Start-Class': 'bond.Main'
    }
    doLast {
        println "Built $archiveFileName ${project(':simple-bond-issuer').version}"
    }
}
