plugins {
    id 'org.springframework.boot' version '2.4.10'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'

    // used to set version of project as git hash
    id 'com.palantir.git-version' version '0.12.2'
}

version = gitVersion()

configurations {
    implementation {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

def lombokVersion = '1.18.20'

dependencies {

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    implementation 'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-log4j2',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.springframework.boot:spring-boot-starter-validation',
            'org.springframework.boot:spring-boot-starter-amqp',
            'org.springframework.boot:spring-boot-starter-actuator',
            'io.springfox:springfox-swagger-ui:2.9.2',
            'io.springfox:springfox-swagger2:2.9.2',
            'com.integralblue:log4jdbc-spring-boot-starter:2.0.0',
            'org.hibernate:hibernate-core:5.4.8.Final',
            'com.h2database:h2:1.4.200'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
    archiveFileName = 'bond-issuer.jar'
    mainClassName 'bond.Main'
    manifest {
        attributes 'Start-Class': 'bond.Main'
    }
    doLast {
        println "Built $archiveFileName with version: ${project(':simple-bond-issuer').version}"
    }
}

springBoot {
    // spring boot plugin injects the version and build information into actuator /info endpoint
    buildInfo()
}